buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'


checkstyle {
    toolVersion = "8.7"
}



repositories {
    maven { url 'https://maven.fabric.io/public' }
}

check.dependsOn 'lint'


android {
    lintOptions {
        abortOnError false
        xmlReport false
        htmlReport true

        lintConfig rootProject.file('config/lint/lint.xml')
        htmlOutput rootProject.file('reports/lint/lint-result.html')
        xmlOutput rootProject.file('reports/lint/lint-result.xml')
    }
    signingConfigs {
        config {
            keyAlias 'eningkey'
            keyPassword 'eningapps'
            storeFile file('eningkey')
            storePassword 'eningapps'
        }
    }
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.tseluikoartem.ening.yandexmobdevproject"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "5.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "1b47357324b947eba1dfd8980363bc16"]
    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            ext.betaDistributionGroupAliases = 'tseluiko-yandexmobdev'
        }
    }
    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
        }
        unitTests {
            includeAndroidResources = true
        }
    }
}

task checkstyle(type: Checkstyle) {
    description 'Checks if the code is somewhat acceptable'
    group 'verification'

    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    configProperties.checkstyleSuppressionsPath = rootProject.file('config/checkstyle/suppressions.xml')

    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'

    // empty classpath
    classpath = files()
}

task findbugs(type: FindBugs) {
    description 'Run FindBugs'
    group 'verification'

    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File('../config/findbugs/findbugs-exclude-filter.xml')
    classes = rootProject.files('app/build/intermediates/classes')

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination rootProject.file('reports/findbugs/findbugs.xml')
        }
        html {
            destination rootProject.file('reports/findbugs/findbugs.html')
        }
    }

    classpath = files()
}

task pmd(type: Pmd) {
    description 'Run PMD'
    group 'verification'

    ruleSetFiles = rootProject.files('config/pmd/pmd-rules.xml')
    ruleSets = []
    ignoreFailures = false

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination rootProject.file('reports/pmd/pmd.xml')
        }
        html {
            destination rootProject.file('reports/pmd/pmd.html')
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:3.0.1'

    compile 'com.yandex.android:mobmetricalib:2.80'
    implementation 'com.android.support:cardview-v7:26.1.0'
    compile 'net.hockeyapp.android:HockeySDK:5.1.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }
    implementation 'com.android.support:gridlayout-v7:26.1.0'

    implementation "android.arch.lifecycle:extensions:1.0.0"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0"

    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"


    testImplementation 'org.mockito:mockito-core:2.15.0'
    testImplementation 'org.robolectric:robolectric:3.7'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.8.1'
    androidTestImplementation 'org.mockito:mockito-android:2.15.0'

    implementation 'com.yandex.android:mobmetricalib:2.80'
    implementation 'com.yandex.android:mobmetricapushlib:1.0.0'
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'
    implementation 'com.google.android.gms:play-services-base:11.8.0'

}
apply plugin: 'com.google.gms.google-services'